from __future__ import (absolute_import, division, print_function)
import os
Import('runtime')

# TODO(mcj) should this import a stripped-down environment from the SConstruct?
env = Environment(ENV = os.environ)
if GetOption('clang'):
    env['CC'] = 'clang'
    env['CXX'] = 'clang++'

env.Append(CPPPATH = [os.path.join(Dir('.').srcnode().up().abspath, 'include')])
env.Append(CPPFLAGS = ['-Wall', '-Werror', '-O3', '-gdwarf-3',])
env.Append(CPPDEFINES = ['NASSERT', 'NDEBUG'])
env.Append(CXXFLAGS = ['-std=c++14'])
env.Append(CFLAGS = ['-std=c11'])

# FIXME(mcj) libsimalloc is identical for all runtimes, but libswarm differs per
# runtime. We should build the former only once.
libsimalloc = env.StaticLibrary(target='simalloc', source=['simalloc.c'])

swarmenv = env.Clone()
swarmenv.Append(CPPDEFINES = [
        (runtime.upper() if runtime in ['seq', 'tls', 'oracle'] else 'SWARM')
        + '_RUNTIME'
        ])

if runtime == 'tls':
    swarm_source = ['tls_runtime.cpp']
else:
    assert runtime in ['swarm', 'seq', 'oracle', 'scc', 'scc_serial', 'competition'], 'Unknown runtime'
    swarm_source = []
libswarm = swarmenv.StaticLibrary(target='swarm', source=swarm_source) if swarm_source else None

Return('libswarm', 'libsimalloc')
